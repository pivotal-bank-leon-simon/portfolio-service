buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
        springCloudServicesVersion = '2.0.2.RELEASE'
        springCloudVersion = 'Finchley.SR2'
    }

    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.7"
    }
}

ext['spring-security.version'] = '5.1.1.RELEASE'

repositories {
    mavenLocal()
    mavenCentral()
}

// Apply versioning to all projects from versioning script.
apply plugin: 'java'
apply plugin: 'jacoco'
apply from: "$projectDir/gradle/versioning.gradle"
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: "io.spring.dependency-management"
apply plugin: "com.gorylenko.gradle-git-properties"

println version

test { finalizedBy jacocoTestReport }

task manifestFilter(type: Copy) {
    from 'manifest-unversioned.yml'
    into "${buildDir}/"
    rename { String fileName ->
        fileName.replace('-unversioned', '')
    }
    expand(version: "${version}")
}

assemble { finalizedBy manifestFilter }

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
        xml.enabled false
    }

    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}


jacoco {
    toolVersion = "0.7.5+" //	reportsDir = file("$buildDir/customJacocoReportDir")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
    maven { url "http://repo.spring.io/milestone" }
    maven { url "http://repo.spring.io/snapshot" }
}

dependencyManagement {
    imports {
        mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:${springCloudServicesVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom 'org.springframework.cloud:spring-cloud-stream-dependencies:Fishtown.RC2'
    }
}

dependencies {

    // shared dependencies for all services
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.cloud:spring-cloud-starter-sleuth")
    implementation("io.pivotal.spring.cloud:spring-cloud-services-starter-config-client")
    implementation("io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry")
    implementation("io.pivotal.spring.cloud:spring-cloud-services-starter-circuit-breaker")
    implementation("org.springframework.security:spring-security-data")
    implementation("org.springframework.security:spring-security-oauth2-resource-server")
    implementation("org.springframework.security:spring-security-oauth2-core")
    implementation("org.springframework.security:spring-security-oauth2-client")
    implementation("org.springframework.security:spring-security-oauth2-jose")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation('org.springframework.cloud:spring-cloud-stream')
    implementation('org.springframework.cloud:spring-cloud-starter-stream-rabbit')
    runtimeOnly("org.springframework.boot:spring-boot-devtools")
    compileOnly("com.newrelic.agent.java:newrelic-api:4.8.0")
    //test packages
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.springframework.security:spring-security-test:5.1.1.RELEASE")
    testImplementation("com.jayway.jsonpath:json-path")
    // Runtime
    runtime "org.hsqldb:hsqldb"
    runtime "mysql:mysql-connector-java:5.1.47"

}

// process substitutions in yml files.
processResources {
    include "**/*.yml"
    filesNotMatching("**/application.yml") { expand(project.properties) }
}

jar { baseName = 'portfolio' }
description = "Micro-service to manage portfolios"

task wrapper(type: Wrapper) { gradleVersion = '5.1' }
